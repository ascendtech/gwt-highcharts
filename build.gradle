defaultTasks 'build'

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'

    defaultTasks 'build'

    version = '1.0'

    repositories {
        mavenCentral()
        flatDir name: 'directoryRepository', dirs: rootProject.file('notInMaven')
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    configurations.all {
        // check for updates every build more than 10 minutes apart (for snapshots)
        resolutionStrategy.cacheChangingModulesFor 600, 'seconds'
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    compileJava {
        options.debug = true
        options.debugOptions.debugLevel = "source,lines,vars"
    }

    dependencies {
        testCompile 'junit:junit:[4.10,)'
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/java'
            }
        }
        test {
            resources {
                srcDir 'src/test/java'
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

}
